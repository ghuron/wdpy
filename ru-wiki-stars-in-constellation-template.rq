SELECT ?line WITH { 
  SELECT DISTINCT ?constellation ?item ?line ?row {
  {
    { SELECT * {
        BIND (wd:{0} AS ?constellation)
        ?constellation wdt:P1813 ?short.
        ?item wdt:P59 ?constellation; p:P528[ps:P528 ?b; pq:P972 wd:Q105616; wikibase:rank wikibase:NormalRank]
        VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
        FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
        FILTER NOT EXISTS { [] p:P527[ps:P527 ?item; pq:P1545 []] }
    } }
    OPTIONAL { [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru }
    BIND (REPLACE(?b, CONCAT(' ', STR(?short)), '') AS ?bayer)
    BIND (IF(BOUND(?ru), CONCAT('* [[', ?ru, '|', ?bayer, ']]'), CONCAT('* {{нет статьи|', REPLACE(STR(?item), 'http://www.wikidata.org/entity/', ''), '|',?bayer, '|Звезда}}')) AS ?line)
    BIND (CONCAT('A', REPLACE(IF(REGEX(?bayer, '^[A-Za-z]'), CONCAT('ωω',?bayer), IF(REGEX(?bayer, '\\s[A-Za-z][A-Za-z][A-Za-z]'), CONCAT('ωωω',?bayer), ?bayer)),'¹','1')) AS ?row)
  } UNION {
    BIND (wd:{0} AS ?constellation)
    ?constellation wdt:P1813 ?short.
    ?item wdt:P59 ?constellation; p:P528[ps:P528 ?f; pq:P972 wd:Q111116; wikibase:rank wikibase:NormalRank]
    VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
    FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
    OPTIONAL { [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru }
    BIND(REPLACE(?f, CONCAT(' ', STR(?short)), '') AS ?flam)
    BIND(IF(BOUND(?ru), CONCAT('* [[', ?ru, '|', ?flam, ']]'), CONCAT('* {{нет статьи|', REPLACE(STR(?item), 'http://www.wikidata.org/entity/', ''), '|', ?flam, '|Звезда}}')) AS ?line)            
    BIND(xsd:integer(?flam) as ?num)
    BIND(CONCAT('BB', IF(BOUND(?num), SUBSTR(CONCAT('00', ?flam), STRLEN(?flam)), CONCAT('B', SUBSTR(CONCAT('00', ?flam), STRLEN(?flam)-4)))) AS ?row)
  } UNION {
    BIND (wd:{0} AS ?constellation)
    ?constellation wdt:P1813 ?short.
    ?item wdt:P59 ?constellation; wikibase:sitelinks ?iw; p:P528[ps:P528 ?v; pq:P972 wd:Q222662; wikibase:rank wikibase:NormalRank]
    VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
    FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
    OPTIONAL { [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru }
    BIND(REPLACE(?v, CONCAT(' ', STR(?short)), '') AS ?var)
    FILTER(REGEX(?var, '[A-Z].*') && (?iw > 0))
    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q3937 }
    BIND(IF(BOUND(?ru), CONCAT('* [[', ?ru, '|', ?var, ']]'), CONCAT('* {{нет статьи|', REPLACE(STR(?item), 'http://www.wikidata.org/entity/', ''), '|', ?var, '|Звезда}}')) AS ?line)            
    BIND(CONCAT(IF(REGEX(?var, '^[R-Z]+$'), 'CC', 'CD'), STR(STRLEN(?var)), ?var) AS ?row)
  } UNION {
    { 
      SELECT DISTINCT ?constellation ?item ?itemLabel {
      {
        BIND (wd:{0} AS ?constellation)
        ?item wdt:P59 ?constellation; wdt:P398 [] # fictional planets should be deprecated
      } UNION {
        BIND (wd:{0} AS ?constellation)
        ?item wdt:P59 ?constellation; wdt:P527/wdt:P398 [] # in multiple systems exoplanet are associated with concrete star
      }
      VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
      FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
      FILTER NOT EXISTS { [] p:P527[ps:P527 ?item; pq:P1545 []] }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en" }
    } }
    OPTIONAL { [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru }
    BIND(IF(BOUND(?ru), CONCAT('* [[', ?ru, ']]'), CONCAT('* {{нет статьи|', REPLACE(STR(?item), 'http://www.wikidata.org/entity/', ''), '|', 
                                                          CONCAT(UCASE(SUBSTR(?itemLabel, 1, 1)), SUBSTR(?itemLabel, 2)), '|Звезда}}')) AS ?line)    
    BIND(IF(BOUND(?ru), ?ru, ?itemLabel) AS ?row1)
    BIND(IF(STRSTARTS(?row1, 'HD '), CONCAT('HD', SUBSTR(REPLACE(?row1, 'HD ', '000000000'), STRLEN(?row1))), ?row1) AS ?row2)
    BIND(IF(STRSTARTS(?row2, 'HAT-P-'), CONCAT('HATP', SUBSTR(REPLACE(?row2, 'HAT-P-', '000000000'), STRLEN(?row2))), ?row2) AS ?row3)
    BIND(IF(STRSTARTS(?row3, 'WASP-'), CONCAT('WASP', SUBSTR(REPLACE(?row3, 'WASP-', '000000000'), STRLEN(?row3))), ?row3) AS ?row4)
    BIND(CONCAT('DD', ?row4) AS ?row)  } }
} AS %Q {
  {
    VALUES (?row ?line) { 
      ('A1' '{{Навигационная таблица')
      ('A2' ' |имя           = {{subst:PAGENAME}}')
      ('A3' ' |state         = <includeonly>{{{state|autocollapse}}}</includeonly>')
      ('A5' ' |класс_списков = hlist hlist-items-nowrap')
      ('A6' '')
      ('A7' ' |заголовок1    = [[Обозначения Байера|Байер]]')
      ('A8' ' |список1       =')
      ('A9' '') 
      ('B1' '') 
      ('B2' ' |заголовок2    = [[Обозначения Флемстида|Флемстид]]') 
      ('B3' ' |список2       =') 
      ('B4' '')
      ('C1' '') 
      ('C2' ' |заголовок3    = [[Переменная звезда|Переменные]]') 
      ('C3' ' |список3       =') 
      ('C4' '')
      ('D1' '') 
      ('D2' ' |заголовок4    = [[Планетная система|Планетные<br>системы]]') 
      ('D3' ' |список4       =') 
      ('D4' '')
      ('E1' '') 
      ('E2' ' |заголовок5    = Другие') 
      ('E3' ' |список5       =') 
      ('E4' '')
      ('F1' '') 
      ('F3' ' }}<noinclude>')
      ('F4' '[[Категория:Навигационные шаблоны:Звёзды по созвездиям]]') 
      ('F5' '</noinclude>')
    }  
  } UNION {
    BIND ('A4' as ?row)
    BIND (wd:{0} AS ?constellation) 
    [] schema:about ?constellation; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ruconst.
    ?item p:P360[ps:P360 wd:Q523; pq:P59 ?constellation].
    [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?rulist
    BIND(CONCAT(' |заголовок     = Звёзды созвездия [[', ?ruconst, '|', REPLACE (?rulist, 'Список звёзд созвездия ', ''), ']]') AS ?line)
  } UNION {
    INCLUDE %Q
  } UNION {
    { SELECT DISTINCT ?ru ?item ?itemLabel {
      BIND (wd:{0} AS ?constellation) 
      ?constellation wdt:P1813 ?short.
      ?item wdt:P59 ?constellation.
      VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
      FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
      [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru
      MINUS { INCLUDE %Q } # only those that was not listed above
      FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q3937 }
      FILTER NOT EXISTS { ?item wdt:P3208 [] } # exclude NGC articles (low quality)
      FILTER NOT EXISTS { ?item wdt:P397 [] } # exclude exoplanet
      SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en" }
    } }
    BIND(CONCAT('* [[', ?ru, ']]') AS ?line)    
    BIND(IF(STRSTARTS(?ru, 'HD '), CONCAT('HD', SUBSTR(REPLACE(?ru, 'HD ', '000000000'), STRLEN(?ru))), ?ru) AS ?row1)
    BIND(CONCAT('EE', ?row1) AS ?row)
  } UNION {
    BIND (wd:{0} AS ?constellation) 
    BIND ('F2' as ?row)
    ?list p:P360[ps:P360 wd:Q523; pq:P59 ?constellation].
    [] schema:about ?list; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?rulist
    BIND(CONCAT(' | внизу        = [[', ?rulist, ']]') AS ?line)
  }
} ORDER BY ?row
